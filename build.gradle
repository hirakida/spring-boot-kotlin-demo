buildscript {
    ext {
        kotlinVersion = '1.2.31'
        springBootVersion = '2.0.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
    }
}

allprojects {
    group 'com.example'
    version '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.8

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
            javaParameters = true
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
        }
        dependencies {
            dependency "org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}"
            dependency "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
            dependency 'org.webjars:bootstrap:4.0.0'
            dependency 'org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.1'
            dependency 'org.mybatis:mybatis-typehandlers-jsr310:1.0.2'
        }
    }

    dependencies {
        compile('org.jetbrains.kotlin:kotlin-stdlib-jre8')
        compile('org.jetbrains.kotlin:kotlin-reflect')
        compile('com.fasterxml.jackson.module:jackson-module-kotlin')
        runtime('com.h2database:h2')
    }
}

project(':jpa') {
    jar {
        baseName = 'jpa-demo'
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-data-jpa')
        compile('org.springframework.boot:spring-boot-starter-web')
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-starter-thymeleaf')
        compile('org.webjars:webjars-locator-core')
        compile('org.webjars:bootstrap')
        runtime('org.springframework.boot:spring-boot-devtools')
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }
}

project(':mybatis') {
    jar {
        baseName = 'mybatis-demo'
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-web')
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.mybatis.spring.boot:mybatis-spring-boot-starter')
        compile('org.mybatis:mybatis-typehandlers-jsr310')
        runtime('org.springframework.boot:spring-boot-devtools')
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }
}
